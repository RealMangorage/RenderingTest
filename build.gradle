plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.mangorage'
version = '1.0-SNAPSHOT'

def lwjglVersion = "3.3.3"

def lwjglModules = [
        "lwjgl",
        "lwjgl-glfw",
        "lwjgl-opengl",
        "lwjgl-stb"
]

def lwjglNatives = [
        "natives-windows",
        "natives-windows-x86",
        "natives-linux",
        "natives-linux-arm64",
        "natives-linux-arm32",
        "natives-macos",
        "natives-macos-arm64"
]

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'com.google.code.gson:gson:2.13.1'

    // LWJGL core + modules
    lwjglModules.each { module ->
        implementation "org.lwjgl:$module:$lwjglVersion"
    }

    // LWJGL native runtime dependencies for ALL platforms
    lwjglModules.each { module ->
        lwjglNatives.each { classifier ->
            runtimeOnly "org.lwjgl:$module:$lwjglVersion:$classifier"
        }
    }

    implementation 'org.joml:joml:1.10.5'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.mangorage.game.Start'
        )
    }
}